; script is designed for designed for "Dark and Darker" Lobby Management
; essentially it just clicks the screen in certain places as specified
; designed for 1920x1080 screen in "Dark and Darker" Lobby (log in to your character)
; this is in development and made for my personal use and testing
;   as such - there are no warranties or gaurentees
;           - use at your own risk
; file main use: define useful clicker functions and definitions
; written and maintained by matthewjearly@gmail.com

#include LobbyButtonFunctionsLibrary.ank

; all locations should probably be defined like this for ease of updating (todo)
; merchant loc notes:
;    270 column 1, 500 col 2, 730, 970, 1200, 1420, 1650
;    300 row 1, 600 row 2, 900 

global AlchemistLoc       := [270, 300]
global ArmourerLoc        := [500, 300]
global CockatriceLoc      := [730, 300]
global ExpressmanLoc      := [1200, 300]
global GoblinLoc          := [1650, 300]
global GoldsmithLoc       := [270, 600]
global LeathersmithLoc    := [740, 600]
global SquireLoc          := [1200, 600]
global SurgeonLoc         := [1420, 600]
global TailorLoc          := [1650, 600]
global CollectorLoc       := [507, 900]
global SmithLoc           := [970, 900]
global WoodsmanLoc        := [1200, 900]


global ThirdCharDownLoc   := [1600, 300]
global OptionsLoc         := [219, 599]


; mini sleep
MS() {
  sleep 21 ; to help with speeding up tool
}

; short sleep
SS() {
  sleep 179 ; probably needs to be higher 
}

; long sleep
LS() { 
  sleep 459  ; probably needs to be higher 
}

; very long sleep
VLS() {
  sleep 1999  ; can take some characters a very long time to load in, 4200ms might be too short
}

; aux function  : todo: this is not perfect and still bugs sometimes
TripleOffsetLeftClick_dnd_ui_bug(loc1, loc2) {
  MouseClick "left", loc1, loc2
  MS()
  MouseClick "left", loc1, loc2 - 1
  MS()
  MouseClick "left", loc1 - 1, loc2
}

HoverMouseThenClick(loc1, loc2) {
  MouseMove loc1, loc2
  LS()
  MouseClick "left", loc1, loc2
}

; aux function
GlobalBack() {
  global
  Loop 2 {
    MouseClick "left", 200, 40   ; play or leave trade button, twice to double back some menus
    LS()
  }
  MouseClick "left", 858, 617  ; click 'are you sure' market button (or some merchant possibly)
  LS()
}

; aux function
ClickEntireInventory() {

  ; click every cell
  BeforeFirstCell := [1420, 600]
  CellSize := 41
  Loop 10 {
    val1 := BeforeFirstCell[1] + A_Index * CellSize
    Loop 5 {
      val2 := BeforeFirstCell[2] + A_Index * CellSize
      MouseClick "left", val1, val2
      MS()
    }
  }
}

; aux function
MarketplaceBuyCheapest() {
  MouseClick "left", 1789, 350  ; buy first (cheapest) item
  Loop 2 {
    LS()
  }
  MouseClick "left", 954, 763  ; fill all button
}

; aux function
FillAllMakeDeal() {
  MouseClick "left", 966, 929  ; click on fill all from stash
  LS()
  MouseClick "left", 963, 992  ; click make deal
}

; aux function
UnequipKit() {
  MouseClick "right", 650, 250 ; primary 1
  MS()
  MouseClick "right", 715, 250 ; primary 2
  MS()
  MouseClick "right", 1015, 250  ; secondary 1
  MS()
  MouseClick "right", 1066, 250   ; secondary 2
  MS()
  MouseClick "right", 860, 350   ; chest
  MS()
  MouseClick "right", 860, 500   ; legs
  MS()
  MouseClick "right", 958, 380   ; back
  MS()
  MouseClick "right", 855, 230   ; helm
  MS()
  MouseClick "right", 760, 525   ; gloves
  MS()
  MouseClick "right", 960, 524   ; feet

    ; rings and ammy? leave it for now i guess
}

HitPlay() {
  GlobalBack()
  MouseClick "left", 238, 35  ; top play button
  LS()
  MouseClick "left", 181, 981  ; start bottom left button
  LS()
  MouseClick "left", 780, 675  ; timmy yes button
}

GoToClassSpec() {
  GlobalBack()
  MouseClick "left", 720, 35  ; top class button
  LS()
  MouseClick "left", 983, 118  ; perks and skills button
}

GoToGoldSmith() {
  GlobalBack()
  MouseClick "left", 982, 31  ; top merchant button
  SS()
  MouseClick "left", GoldsmithLoc[1], GoldsmithLoc[2]
}

GoToAlchemist() {
  GlobalBack()
  MouseClick "left", 982, 31  ; top merchant button
  SS()
  MouseClick "left", AlchemistLoc[1], AlchemistLoc[2]
}

GoToLeatherSmithService() { 
  GlobalBack()
  MouseClick "left", 982, 31  ; top merchant button
  LS()
  MouseClick "left", LeathersmithLoc[1], LeathersmithLoc[2]
  SS()
  MouseClick "left", 303, 141  ; Service button
}

GoToAlchemistService() { 
  GoToAlchemist()
  SS()
  MouseClick "left", 303, 141  ; Service button
}

GoToTailorService() { 
  GlobalBack()
  MouseClick "left", 982, 31  ; top merchant button
  LS()
  MouseClick "left", TailorLoc[1], TailorLoc[2]
  SS()
  MouseClick "left", 303, 141  ; service button
}

GoToWeaponsmithService() { 
  GlobalBack()
  MouseClick "left", 982, 31  ; top merchant button
  LS()
  MouseClick "left", SmithLoc[1], SmithLoc[2]
  SS()
  MouseClick "left", 303, 141  ; Service button
}

GoToArmorerService() { 
  GlobalBack()
  MouseClick "left", 982, 31  ; top merchant button
  LS()
  MouseClick "left", ArmourerLoc[1], ArmourerLoc[2]
  SS()
  MouseClick "left", 303, 141  ; service button
}

GoToGoblinBuy() {
  GlobalBack()
  MouseClick "left", 982, 31  ; top merchant button
  LS()
  MouseClick "left", GoblinLoc[1], GoblinLoc[2]  ; goblin
}

GoToCockatrice(){
  GlobalBack()
  MouseClick "left", 982, 31  ; top merchant button
  LS()
  MouseClick "left", CockatriceLoc[1], CockatriceLoc[2] 
}

GoToSquireAndFill() {
  GlobalBack()
  MouseClick "left", 982, 31  ; top merchant button
  LS()
  MouseClick "left", SquireLoc[1], SquireLoc[2]
  LS()
  MouseClick "left", 1125, 992  ; click to the right of equip base gear button first (on nothing), because sometimes it bugs and the equip base gear button will not register until you click somewhere else on the screen
  SS()
 
  ; unequip tool slots
  MouseClick "right", 1400, 456
  MS()
  MouseClick "right", 1400, 518
  MS()
  MouseClick "right", 1400, 575
  MS()
  MouseClick "right", 1830, 456
  MS()
  MouseClick "right", 1830, 518
  MS()
  MouseClick "right", 1830, 575
  MS()

  ; unequip right weapon right side
  ;  - the point is to unequip a bow so that it gives you more arrows/bolts
  ;  - not for all characters maybe change it or add another button option
  ; MouseClick "right", 1823, 250
  ; MS()


  SS()
  MouseClick "left", 955, 992 ; click Equip Base Gear
  MS()
  MouseClick "left", 953, 991 ; click Equip Base Gear again (sometimes doesn't register first time)

}

FullSquireReset() {
  ; get ride of inventory
  DoubleSellInventoryFullConfirm()
  
  LS()

  ; unequip kit 
  GoToStash()
  LS()
  UnequipKit()

  LS()

  ; sell kit
  VendorEntireInventoryOfNonCollectables()
  LS()
  MouseClick "left", 963, 992  ; click make deal

  LS()
  

  GoToSquireAndFill()
}


GoToSellAtVendor() {
  GlobalBack()
  GoToAlchemist()
  SS()

  ;  DISCLAIMER IF UR READING THIS CODE
  ;  I don't know what is wrong with this dang "sell" button, but it just won't register sometimes.
  ;  That is why we click all over the screen here, trying to get it to realize that the sell button is clickable afterall. 
  ;  This problem seems to be a bug with the game's interface

  ; Various non-sense clicks to try to fix ironmace bug
  MouseClick "left", 463, 205  ; click to the right first because sometimes it bugs the button is unclickable until you click somewhere else
  MS()
  MouseClick "left", 462, 204  ; and again because wut is wrong with this thing
  MS()   
  MouseClick "left", 1125, 992  ; click to the right of equip base gear button first (on nothing), because sometimes it bugs and the equip base gear button will not register until you click somewhere else on the screen
  SS()
    
  MouseClick "left", 333, 205  ; sell loc
}

GoToSurgeon() {
  GlobalBack()
  MouseClick "left", 982, 31  ; top merchant button
  LS()
  MouseClick "left", SurgeonLoc[1], SurgeonLoc[2]
}

GoToMarketplaceSearch() {
  global
  GlobalBack()
  MouseClick "left", 1192, 31  ; top trade button
  LS()
  MouseClick "left", 1191, 247  ; marketplace button location
  Loop 2 {
    LS()
  }
  MouseClick "left", 117, 203  ; search dropdown button
}

GoToMarketplace() {
  global
  GlobalBack()
  MouseClick "left", 1192, 31  ; top trade button
  LS()
  MouseClick "left", 1191, 247  ; marketplace button location
}

GoToCollector() {
  GlobalBack()
  MouseClick "left", 982, 31  ; top merchant button
  LS()
  MouseClick "left", CollectorLoc[1], CollectorLoc[2]
}

GoToWoodsman() {
  GlobalBack()
  MouseClick "left", 982, 31  ; top merchant button
  LS()
  MouseClick "left", WoodsmanLoc[1], WoodsmanLoc[2]
}

GoToStash() {
  GlobalBack()
  MouseClick "left", 877, 44  ; top stash button
}

ExpressmanExpress() {
  GlobalBack()
  MouseClick "left", 982, 31  ; top merchant button
  LS()
  MouseClick "left", ExpressmanLoc[1], ExpressmanLoc[2]  ; expressman button
  LS()
  MouseClick "left", 306, 1011  ; select all button
  SS()
  MouseClick "left", 947, 896  ; fill all
  SS()
  MouseClick "left", 954, 964  ; get item
}

GetAllFromGoblin() {
  GoToGoblinBuy()
  LS()
  MouseClick "left", 296, 139  ; Service button
  LS()
  MouseClick "left", 306, 1011  ; select all button
  SS()
  MouseClick "left", 947, 896  ; fill all
  SS()
  MouseClick "left", 954, 964  ; get item
}

CockatriceGather() {
  GoToCockatrice()
    LS()
  MouseClick "left", 296, 139  ; Service button
  LS()
  MouseClick "left", 306, 1011  ; select all button
  SS()
  MouseClick "left", 947, 896  ; fill all
  SS()
  MouseClick "left", 954, 964  ; get item
}

BuyPickaxe() { 
  GoToWoodsman()
  LS()
  MouseClick "left", 180, 335  ; second pick over
  LS()
  FillAllMakeDeal()
}

BuyBandages() {
  GoToSurgeon()
  LS()
  MouseClick "left", 70, 300  ; click triple grey bandage
  LS()
  Loop 5 {   ; buy this+1
    MouseClick "left", 1013, 604  ; click on increase
    MS()
  }
  SS()
  FillAllMakeDeal()
}

BuyALotOfBandages() {
  GoToSurgeon()
  LS()
  MouseClick "left", 70, 300  ; click triple grey bandage
  LS()
  Loop 5 {   ; buy this+1
    MouseClick "left", 1013, 604  ; click on increase
    MS()
  }
  Loop 2 { 
    SS()
    FillAllMakeDeal()
  }
}

BuyPotions() {
  GoToAlchemist()
  LS()

  ; default set to buy 6 grey quality potions
  MouseClick "left", 70, 300  ; click low quality potion
  LS()
  Loop 5 {  ; click increase
    MouseClick "left", 1013, 604 
    MS()
  }
  FillAllMakeDeal()
}

BuyALotOfPotions() {
  GoToAlchemist()
  LS()
  ; default set to buy 12 grey quality potions
  MouseClick "left", 70, 300  ; click low quality potion
  LS()
  Loop 5 {   ; buy this+1
    MouseClick "left", 1013, 604  ; click on increase
    MS()
    MS()
  }
  Loop 2 {
    SS()
    FillAllMakeDeal()
  }
}

; this will attempt to switch to your previous character
; in other words, it will go to the character you last previously selected before the one you are currently on
HotswapCharacters() {
  GlobalBack()
  ; log out of character
  MouseClick "left",  237, 44   ; click play button
  LS()
  MouseClick "left", 1858, 1025  ; change class button
  Loop 2 {
    VLS()
  }
  MouseClick "left", 1605, 280  ; click on 2nd character down
  LS()
  MouseClick "left", 954, 979  ; Enter the lobby button
}

VendorEntireInventoryOfCollectables() {
  GoToCollector()  ; starts with GlobalBack
  SS()
  ClickEntireInventory()
}

VendorEntireInventoryOfNonCollectables() {
  GoToSellAtVendor()
  SS()
  ClickEntireInventory()
} 

; Sells your inventory with NO confirmation (Dangerous)
; this dumps your inventory to vendors
; does not process silver
DoubleSellInventoryFullConfirm() {
  ; step 1: get rid of collectables
  VendorEntireInventoryOfCollectables()
  LS()
  MouseClick "left", 963, 992  ; click make deal
  LS()
  
  ; step 2: get ride of everything else
  VendorEntireInventoryOfNonCollectables()
  LS()
  MouseClick "left", 963, 992  ; click make deal
  
}

; use when in stash to move entire inventory in one go
TransferInventory() {
  ; click every cell
  TransferInventory_LocBeforeFirstCell := [667, 601]
  TransferInventory_CellSize := 41
  Send "{Shift down}"
  Loop 10 {
    val1 := TransferInventory_LocBeforeFirstCell[1] + A_Index * TransferInventory_CellSize
    Loop 5 {
      val2 := TransferInventory_LocBeforeFirstCell[2] + A_Index * TransferInventory_CellSize
      MouseClick "right", val1, val2
      MS()
      MS()
    }
  }
  Send "{Shift up}"
}


; nothing is bound to this, probably don't use it, it just buys everything from the goblin
FullGamble() {
  GoToGoblinBuy()
  LS()
  MouseClick "left", 71, 290  ; THROW AWAY CLICK / ENABLE INTERFACE CLICK
  MS()
  loop 10 {
    MouseClick "left", 73, 291
    LS()
    FillAllMakeDeal()
    LS()
  }
}

; squire sets
GoToSets() {
  GlobalBack()
  MouseClick "left", 982, 31  ; top merchant button
  LS()
  MouseClick "left", SquireLoc[1], SquireLoc[2]
  LS()
  MouseClick "left", 420, 201  ; click My Gear Sets area
  SS()
}

ToolBeltAndRequeue() {
  GoToSquireAndFill()
  LS()
  HitPlay()
}

; invest from merchants function (level alch and surgeon first)
BuyGreenPotionsAndBandagesAndLuckPotion() {
  ; PART 1, BUY ALL GREEN POTIONS
  GoToAlchemist()
  LS()
  MouseClick "left", 281, 302 ; set of triple green potions
  LS()
  FillAllMakeDeal()
  LS()
  MouseClick "left", 157, 302 ; set of single green potions
  LS()
  Loop 2 {  ; click increase
    MouseClick "left", 1013, 604 
    MS()
  }
  FillAllMakeDeal()
  LS()

  ; PART 2, BUY ALL GREEN BANDAGES
  GoToSurgeon()
  LS()
  MouseClick "left", 281, 302 ; set of triple green Bandages
  LS()
  FillAllMakeDeal()
  LS()

  MouseClick "left", 157, 302 ; set of single green Bandages
  LS()
  Loop 2 {  ; click increase
    MouseClick "left", 1013, 604 
    MS()
  }
  FillAllMakeDeal()

    ; PART 3, BUY LUCK POTION

}

; press esc first and then this
SetVolumeToZero() {

; click options
    MouseClick "left", optionsloc[1], optionsloc[2] 
    MS()
; click audio 1030, 40
    MouseClick "left", 1030, 40 
    MS() 
    MS()
; click zero area 890, 212
    MouseClick "left", 890, 212 
    MS() 
    MS()
    ; click apply
    MouseClick "left", 1060, 1040 
    MS() 
    MS()
}

; press escape first and then this
SetVolumeToFifty() {
; click options
    MouseClick "left", optionsloc[1], optionsloc[2] 
    MS() 
    MS()
; click audio 1030, 40
    MouseClick "left", 1030, 40 
    MS() 
    MS()
; click fifty area 998, 215
    MouseClick "left", 998, 215 
    MS() 
    MS()
; click apply
    MouseClick "left", 1060, 1040 
    MS() 
    MS()
}

; 0 no preference, 1 rare, 2 epic, 3 legendary, 4 unique, 5 artifact
SetMarketPlaceSearchRarityLevel(itemrarity:=0) {
  MouseClick "left", 365, 203  ; search rarity button
  Loop 3 {
    SS()
  }
  switch itemrarity 
  {
    Case 1:
      TripleOffsetLeftClick_dnd_ui_bug(294, 356) ; rare location
    Case 2:
      TripleOffsetLeftClick_dnd_ui_bug(294, 382)  ; epic button
    Case 3:
      TripleOffsetLeftClick_dnd_ui_bug(294, 408)  ; legendary
    Case 4:
      TripleOffsetLeftClick_dnd_ui_bug(294, 434)  ; unique
    Case 5:
      TripleOffsetLeftClick_dnd_ui_bug(294, 460)  ; artifact
  }  
}

; aux Worker func for buying from file
; called from BuyGearOffMarketFromFile
global isQualitySpecified := false
global isMidBuyCycleFromFile := false
PurchaseFromMarket(itemname:="", searchoffset:=0, itemrarity:=0, modifier1:="", modifier2:="") {
  global

  ; STEP 1 - Reset Filters or Go To Market
  if (isMidBuyCycleFromFile) {
    MouseClick "left", 1800, 200  ; Click 'Reset Filters'
  }
  else {
    GoToMarketplace()
  }

  LS()

  ; STEP 2 - Set Rarity (optional)
  if (itemrarity > 0){ 
    SetMarketPlaceSearchRarityLevel(itemrarity)
    isQualitySpecified := 1
    SS()
  }

  ; STEP 3 - Search by Item Name or Any Ring/Neck/Back
  if (itemname == "*Ring" or itemname == "*ring") {
    ; Slot:Ring - for any ring searches
    MouseClick "left", 768, 203
    Send "Ring"
    LS()
    TripleOffsetLeftClick_dnd_ui_bug(768, 277)
  }
  else if (itemname == "*Neck" or itemname == "*neck") {
    ; Slot:neck - for any neck searches
    MouseClick "left", 768, 203
    Send "Neck"
    LS()
    TripleOffsetLeftClick_dnd_ui_bug(768, 277)
  }
  else if (itemname == "*Back" or itemname == "*back") {
    ; Slot:back - for any back searches
    MouseClick "left", 768, 203
    Send "Back"
    LS()
    TripleOffsetLeftClick_dnd_ui_bug(768, 277)
  }
  else {
    MouseClick "left", 117, 203  ; search dropdown button (already clicked if we didn't set rarity first)
    LS()
    Send itemname 
    LS()
    ; a fix for search results not being first ever, such as hounskull search always has golden version first
    searchoffsetMod := 26 * searchoffset
    TripleOffsetLeftClick_dnd_ui_bug(60, 279 + searchoffsetMod)
  }
  
  LS()
  
  ; STEP 4 - Add Modifier (optional)
  if (modifier1 != "") 
  {
    MouseClick "left", 1551, 206 ; random attribute location
    SS()
    Send modifier1
    LS()
    HoverMouseThenClick(1480, 279)
    LS()
  }

  ; STEP 5 - Add Second Modifier (optional)
  if (modifier2 != "") 
  {
    MouseClick "left", 1551, 246 ; random attribute location
    SS()
    Send modifier2
    LS()
    HoverMouseThenClick(1480, 303)   ; location of second thing in attribute search
    LS()
  }

  MouseClick "left", 1790, 277  ; search button
  VLS()     ; wait on search lag

  MarketplaceBuyCheapest()
  VLS()    ; cancel time for user delay
  MouseClick "left", 959, 843  ; complete trade button
  LS()


  ; todo, case of non-avaiable item and it not autobacking
  MouseClick "left", 246, 35   ; back button(good) or leave trade button(bad)
  ; base case correction
  SS()
  MouseClick "left", 1054, 611  ; press no to leave trade button
  
  VLS()   ; wait on completion lag

  ; reset globals
  isQualitySpecified := 0
}


global lastSearchedItem := "na"
RepurchaseAgain() {
    MouseClick "left", 1790, 277  ; search button
  VLS()     ; wait on search lag

  MarketplaceBuyCheapest()
  VLS()    ; cancel time for user delay
  MouseClick "left", 959, 843  ; complete trade button
  LS()


  ; todo, case of non-avaiable item and it not autobacking
  MouseClick "left", 246, 35   ; back button(good) or leave trade button(bad)
  ; base case correction
  SS()
  MouseClick "left", 1054, 611  ; press no to leave trade button
  
  VLS()   ; wait on completion lag

  ; reset globals
  isQualitySpecified := 0
}

; Starter Func to buy from filepath
; halt buying gives us an option to stop an otherwise very long routine
global haltBuyingFromMarket := false
BuyGearOffMarketFromFile(filepath) 
{
  global
  haltBuyingFromMarket := false

  try
    FileObj := FileOpen(filepath, "r")
  catch as Err
  {
    MsgBox "Can't open '" filepath "' for reading." "`n`n" Type(Err) ": " Err.Message
    return
  }

  ; EAT TITLE LINE  
  Line := FileObj.ReadLine()

  ; TRADE FOR THIS LINE
  While (0 = FileObj.AtEOF and haltBuyingFromMarket = false)
  {
    NewLine := FileObj.ReadLine()
    PartOfLine := StrSplit(NewLine, ",")
    size := PartOfLine.Length
    
    if (size > 0 and StrCompare(lastSearchedItem,PartOfLine[1]) = 0 and isMidBuyCycleFromFile) {
      RepurchaseAgain()
      lastSearchedItem := PartOfLine[1]
      continue
    } else {

    }

    if (size = 5)
      PurchaseFromMarket(PartOfLine[1], PartOfLine[2], PartOfLine[3], PartOfLine[4], PartOfLine[5])
    else if (size = 4)
      PurchaseFromMarket(PartOfLine[1], PartOfLine[2], PartOfLine[3], PartOfLine[4])
    else if (size = 3)
      PurchaseFromMarket(PartOfLine[1], PartOfLine[2], PartOfLine[3])
    else if (size = 2)
      PurchaseFromMarket(PartOfLine[1], PartOfLine[2])
    else if (size = 1)
      PurchaseFromMarket(PartOfLine[1])
    else
      continue

    isMidBuyCycleFromFile := true
    lastSearchedItem := PartOfLine[1]
  
  }

  isMidBuyCycleFromFile := false
  FileObj.Close()
}

HaltGearBuying() {
  global
  haltBuyingFromMarket := true
}